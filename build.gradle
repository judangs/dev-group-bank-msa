buildscript {
    ext {
        lombok = "org.projectlombok:lombok"
    }
}

plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

allprojects {
    group = 'org.bank'
    version = '1.0-SNAPSHOT'

    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.maven.apache.org/maven2'
        }
    }
}

subprojects {

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        annotationProcessor lombok
        compileOnly lombok

        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-aop'

        implementation 'com.fasterxml.jackson.core:jackson-databind'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation platform('org.junit:junit-bom:5.10.0')

        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation("org.assertj:assertj-core")
    }

    test {
        useJUnitPlatform()
    }
}

project(':bank-cloud').subprojects {

    ext {
        wiremockVersion = '2.31.0'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.3"
        }
    }

}

project(':bank-application').subprojects {

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }
}